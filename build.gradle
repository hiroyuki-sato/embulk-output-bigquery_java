plugins {
    id "com.github.johnrengelman.shadow" version "5.2.0" apply false
    id "java"
    id "checkstyle"
    id "maven-publish"
    id "org.embulk.embulk-plugins" version "0.4.2"
}

repositories {
    mavenCentral()
    jcenter()
}

group = "io.trocco"
version = "0.0.19"
description = "Bigquery output for embulk"

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
}

dependencies {
    compileOnly "org.embulk:embulk-api:0.10.34"
    compileOnly "org.embulk:embulk-spi:0.10.34"
    compile "org.embulk:embulk-util-timestamp:0.2.1"
    compile "org.embulk:embulk-util-retryhelper:0.8.2"

    compile("org.embulk:embulk-util-config:0.3.1") {
        // They conflict with embulk-core. They are once excluded here,
        // and added explicitly with versions exactly the same with embulk-core:0.10.19.
        exclude group: "com.fasterxml.jackson.core", module: "jackson-annotations"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
        exclude group: "com.fasterxml.jackson.datatype", module: "jackson-datatype-jdk8"
        exclude group: "javax.validation", module: "validation-api"
    }

    // They are once excluded from transitive dependencies of other dependencies,
    // and added explicitly with versions exactly the same with embulk-core:0.10.19.
    compile "com.fasterxml.jackson.core:jackson-annotations:2.6.7"
    compile "com.fasterxml.jackson.core:jackson-core:2.6.7"
    compile "com.fasterxml.jackson.core:jackson-databind:2.6.7"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.6.7"
    compile "javax.validation:validation-api:1.1.0.Final"
    //compile "joda-time:joda-time:2.9.2"

    compile project(path: ":shadow-google-cloud-bigquery-helper", configuration: "shadow")

    // compile "YOUR_JAR_DEPENDENCY_GROUP:YOUR_JAR_DEPENDENCY_MODULE:YOUR_JAR_DEPENDENCY_VERSION"
    testCompile "junit:junit:4.13.2"
    testCompile "org.slf4j:slf4j-simple:1.7.12"
    testCompile 'org.embulk:embulk-junit4:0.10.34'
    testCompile "org.mockito:mockito-core:1.10.19"
    // moved to junit4
    //testCompile "org.embulk:embulk-core:0.10.34:tests"
    testCompile "org.embulk:embulk-deps:0.10.34"
}

embulkPlugin {
    mainClass = "org.embulk.output.bigquery_java.BigqueryJavaOutputPlugin"
    category = "output"
    type = "bigquery_java"
}

publishing {
    publications {
        embulkPluginMaven(MavenPublication) {  // Publish it with "publishEmbulkPluginMavenPublicationToMavenRepository".
            from components.java  // Must be "components.java". The dependency modification works only for it.
        }
    }
    repositories {
        maven {
            url = "${project.buildDir}/mavenPublishLocal"
        }
    }
}

checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '6.14.1'
}
checkstyleMain {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
checkstyleTest {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
task checkstyle(type: Checkstyle) {
    classpath = sourceSets.main.output + sourceSets.test.output
    source = sourceSets.main.allJava + sourceSets.test.allJava
}

gem {
    from("LICENSE")  // Optional -- if you need other files in the gem.
    authors = [ "giwa" ]
    email = [ "ugw.gi.world@gmail.com" ]
    // "description" of the gem is copied from "description" of your Gradle project.
    summary = "Bigquery output plugin for Embulk"
    homepage = "https://github.com/trocco-io/embulk-output-bigquery_java"
    licenses = [ "Apache-2.0" ]
}

gemPush {
    host = "https://rubygems.org"
}
